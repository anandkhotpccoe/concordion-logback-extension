//apply from: 'https://raw.githubusercontent.com/concordion/concordion-extension-build/master/extension-build.gradle'
apply from: 'extension-build.gradle'

description = 'An extension to Concordion that adds Logback logging support'

/*
repositories {
	mavenLocal()
}
*/

ext {
	developers = {
		developer {
			id 'andrew-sumner'
			name 'Andrew Sumner'
			roles { role 'Project owner' }
		}
	}
	inceptionYear = '2014'
}


sourceSets {
    reportlogger
}

dependencies {
	reportloggerCompile 'org.slf4j:slf4j-ext:1.7.28'
    reportloggerCompile 'ch.qos.logback:logback-classic:1.2.3'
    reportloggerCompile 'commons-io:commons-io:2.5'
        
    compile sourceSets.reportlogger.output
    compile sourceSets.reportlogger.compileClasspath
    
	compile "org.concordion:concordion-logging-tooltip-extension:1.1.2"
	compile "org.concordion:concordion-parallel-run-extension:1.1.0"

	// Only needed for javadocs :-(
	reportloggerCompileOnly 'commons-lang:commons-lang:2.6'
}

// build reportlogger jar
task reportloggerJar(type: Jar) {
    from sourceSets.reportlogger.output
}

// Include reportlogger source set in sources jar
sourcesJar  { 
    from sourceSets.reportlogger.allSource
} 

publishing { 
	publications { 
        main(MavenPublication) { 
            artifact reportloggerJar { 
            	classifier 'reportlogger' 
            }
		}
	}
}

test.dependsOn cleanTest
test {
    exclude 'specification/BaseFixture.class'
	systemProperties['concordion.output.dir'] = "$reporting.baseDir/spec"
	systemProperties['concordion.run.threadCount'] = 5
	systemProperties['concordion.extensions'] = "org.concordion.ext.ParallelRunExtension"
	outputs.upToDateWhen { false } // force it to run even if test code hasn't changed
	testLogging {
		events "passed", "skipped", "failed", "standardError"
	}
}
