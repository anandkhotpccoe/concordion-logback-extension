apply from: 'https://raw.githubusercontent.com/concordion/concordion-extension-build/master/extension-build.gradle'
//apply from: 'extension-build.gradle'

description = 'An extension to Concordion that adds Logback logging support'

ext {
	developers = {
		developer {
			id 'andrew-sumner'
			name 'Andrew Sumner'
			roles { role 'Project owner' }
		}
	}
	inceptionYear = '2014'
}


sourceSets {
    integration
}

dependencies {
	//TODO Nigel: Is there a better way to include this in both integration and main source sets?
	integrationCompileOnly 'org.slf4j:slf4j-ext:1.7.21'
    integrationCompileOnly 'ch.qos.logback:logback-classic:1.1.7'
    integrationCompileOnly 'org.concordion:concordion:2.0.2'
    
    compile sourceSets.integration.output
    
	compile 'org.slf4j:slf4j-ext:1.7.21'
	
	compile 'ch.qos.logback:logback-classic:1.1.7'
	compile "org.concordion:concordion-logging-tooltip-extension:1.1.2"
	compile 'org.apache.commons:commons-io:1.3.2'
	
	// Only needed for javadocs :-(
	compileOnly 'commons-lang:commons-lang:2.6'
}

// build integration jar
task integrationJar(type: Jar) {
    from sourceSets.integration.output
}

// Include integration source set in sources jar
sourcesJar  { 
    from sourceSets.integration.allSource
} 
 
// publish integration jar
publishing { 
	publications { 
        main(MavenPublication) {
            artifact integrationJar { 
            	classifier "integration" 
            }
		}
	}
}

test.dependsOn cleanTest
test {
    exclude 'specification/BaseFixture.class'
	systemProperties['concordion.output.dir'] = "$reporting.baseDir/spec"
	outputs.upToDateWhen { false } // force it to run even if test code hasn't changed
	testLogging {
		events "passed", "skipped", "failed", "standardError"
	}
}
